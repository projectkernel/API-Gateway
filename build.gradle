buildscript {
    ext {
        kotlinVersion = '1.2.30'
        springBootVersion = '1.5.9.RELEASE'
    }

    repositories {
        mavenCentral()
        maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.2' }
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
    }
}

group 'kernel'
version '0.1.0'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

repositories {
    mavenCentral()
}

dependencies {
    // Testing
    testCompile("junit:junit")
    testCompile("org.springframework.boot:spring-boot-starter-test")

    // Reflection
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    compile("com.fasterxml.jackson.module:jackson-module-kotlin:2.9.2")

    // Spring
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }
    compile("org.springframework.boot:spring-boot-starter-jetty")
    compile("org.springframework.boot:spring-boot-starter-actuator")

    // HTTP
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile group: 'com.squareup.retrofit2', name: 'converter-gson', version: '2.3.0'


    // GraphQL
    compile group: 'com.graphql-java', name: 'graphql-java', version: '6.0'
    compile group: 'com.graphql-java', name: 'graphql-java-tools', version: '4.3.0'
    compile group: 'com.graphql-java', name: 'graphql-java-servlet', version: '4.7.0'
    compile group: 'com.graphql-java', name: 'graphql-spring-boot-starter', version: '3.10.0'
    compile 'com.graphql-java:graphiql-spring-boot-starter:3.10.0'

    // PACT
    // https://mvnrepository.com/artifact/au.com.dius/pact-jvm-consumer-java8
    compile group: 'au.com.dius', name: 'pact-jvm-consumer-java8_2.12', version: '3.5.13'

}

jar.baseName = 'app'

def generatedOutputDir = "$buildDir/generated/source"

sourceSets {
    generated {
        java.srcDir "${generatedOutputDir}/apollo"
    }

    test {
        java {
            compileClasspath += generated.output
            runtimeClasspath += generated.output
        }
    }
}

compileGeneratedJava {
    classpath = configurations.compile
}

compileJava {
    dependsOn(compileGeneratedJava)
    source += sourceSets.generated.java
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
    source += generatedOutputDir
}